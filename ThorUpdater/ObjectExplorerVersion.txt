Lparameters toUpdateObject
Local ldDate

ldDate = Date(2022, 12, 15)

With m.toUpdateObject
	.VersionDate      = m.ldDate
	.VersionNumber    = '3.3.7.3 - ' + Dtoc(m.ldDate, 1)

	.AvailableVersion = 'Object Explorer-3.3.7.3-update-' + Dtoc(m.ldDate, 1)
	.RegisterWithThor = GetRegisterWithThor(.ApplicationName)
	.Notes			  = GetReleaseNotes()
Endwith

Return m.toUpdateObject


* ================================================================================
* ================================================================================
* ================================================================================

Procedure GetRegisterWithThor(lcAppName)

Local lcAppName, lcClassDefinitions, lcToolCode, lcDefineStatements, lcDescription, lcRegisterWithThor 

* Following indirect sequence is used so that blocks of text (code) can be pasted in from working code.

* The description for the tool
lcDescription	   = GetDescription()

* Any #Define statements (will appear at the beginning of the tool)
lcDefineStatements = GetDefineStatements()

* The code in Proc ToolCode
lcToolCode 		   = GetToolCode()

* somewhat of a misnomer; all procedure and class definitions that follow Proc ToolCode
lcClassDefinitions = GetClassDefinitions()

* This is code to execute after the project has been installed by Thor for the
* first time. Edit this if you want do something different (such as running
* the installed code) or display a different message. You can use code like
* this if you want to execute the installed code; Thor replaces
* ##InstallFolder## with the installation path for the project:
*
* 'do "##InstallFolder##MyProject.prg"'

text to lcRegisterWithThor noshow textmerge
	MessageBox('<<lcAppName>> installed in folder ##InstallFolder##', 0, '<<lcAppName>> Installed', 5000)

* Create the tool under Thor's Tool folder

	loThorInfo = Execscript (_Screen.cThorDispatcher, 'Thor Register=')
	with loThorInfo

* Required properties.

		.PRGName    = 'Thor_Tool_ObjectExplorer'
		.FolderName = '##InstallFolder##'
		.Prompt     = '<<lcAppName>>'

		<< 'Text to .Description NoShow' >>
<<lcDescription>>
		<< 'EndText' >>

* These are used to group and sort tools when they are displayed in menus or the Thor form.

		.Category        = 'Applications'
		.CanRunAtStartUp = .F.

* Options for this tool
		.OptionClasses   = 'clsPublicVariableNameProperties' 
		.OptionTool      = '<<lcAppName>>'

* And the actual code for the tool
		<< 'Text To .DefineStatements Noshow '>>
<<lcDefineStatements>>
		<< 'Endtext' >>

		<< 'Text To .Code Noshow '>>
<<lcToolCode>>
		<< 'Endtext' >>

		<< 'Text To .ClassDefinitions Noshow '>>
<<lcClassDefinitions >>
		<< 'Endtext' >>
		
* Register the tool with Thor.
		llRegister = .Register()
	endwith
endtext

Return m.lcRegisterWithThor 
EndProc


* ================================================================================
* ================================================================================
Procedure GetDescription
	Local lcDescription

	Text To m.lcDescription Noshow
Object Explorer
	Endtext

	Return Evl(m.lcDescription, '')
EndProc


* ================================================================================
* ================================================================================
Procedure GetDefineStatements
	Local lcDefineStatements

	Text To m.lcDefineStatements Noshow PreText 7
	#Define ccTool 'Object Explorer'
	#Define ccKey  'Public Variable Name'
	#Define CRLF   Chr[13] + chr[10]
	EndText 
	Return Evl(m.lcDefineStatements, '')
EndProc


* ================================================================================
* ================================================================================
Procedure GetToolCode
	Local lcCode

	Text To m.lcCode Noshow
	Local lcPublicVariable, loException, loObject

	If Vartype(m.lxParam1) = 'O'
		loObject = m.lxParam1
	Else
		loObject = Sys(1270)
		If Vartype(m.loObject) # 'O'
			Return
		Endif
	Endif

	lcPublicVariable = Nvl(Execscript(_Screen.cThorDispatcher, 'Get Option=', ccKey, ccTool), '')
	If Not Empty(m.lcPublicVariable)
		Try
			Release &lcPublicVariable
			Public &lcPublicVariable
			&lcPublicVariable = m.loObject
		Catch To m.loException
			lcPublicVariable = ''
		Endtry
	Endif

	Do form ('##InstallFolder##Explorer') with m.loObject, , , m.lcPublicVariable

	EndText 
	Return Evl(m.lcCode, '')
EndProc


* ================================================================================
* ================================================================================
Procedure GetClassDefinitions
	Local lcClassDefinitions

	Text To m.lcClassDefinitions Noshow


****************************************************************
****************************************************************
Define Class clsPublicVariableNameProperties As Custom

	Tool		  = ccTool
	Key			  = ccKey
	Value		  = ''
	EditClassName = 'clsPublicVariableName'

Enddefine


****************************************************************
****************************************************************
Define Class clsPublicVariableName As Container

	Procedure Init
		Local lcBodyMarkup, loRenderEngine
		loRenderEngine = Execscript(_Screen.cThorDispatcher, 'Class= OptionRenderEngine')

		lcBodyMarkup =										;
			[   .Class	  = 'Label']						;
			+ [ .Caption  = 'Public Variable Name']			;
			+ [ .AutoSize = .T.]							;
			+ [ .Top	  = (.Top + 8)]						;
			+ [ |]											;
			+ [ .Class = 'TextBox']							;
			+ [ .cTool = ccTool]							;
			+ [ .cKey  = ccKey]								;
			+ [ .Width = 120]								;
			+ [ .Row-Increment = 0]							;
			+ [ .Top = (.Top - 4)]							;
			+ [ |]											;
			+ [ .Class	  = 'Label']						;
			+ [ .Width    = 300]							;
			+ [ .FontSize = 10]								;
			+ [ .Caption  = 'When Object Explorer opens, this variable is created and populated with the value of the object that is displayed.  This variable is released when OE closes.']					;
			+ [ .WordWrap = .T.]							;
			+ [ |]	
	
						
		loRenderEngine.cBodyMarkup = lcBodyMarkup
		loRenderEngine.Render(This, 'Object Explorer')

	Endproc

Enddefine
	
	Endtext

	Return Evl(m.lcClassDefinitions, '')
Endproc


* ================================================================================
* ================================================================================
* ================================================================================

Procedure GetReleaseNotes

Text to lcNote NoShow

### 2022-12-15, Version 3.3.7.4

* Issues related to Thor Configuration and Update
    * Added description in the options page of Thor Configuration
    * Moved most of Updater file contents to Version file.
    * Corrected bugs related to version date as reported in Thor.

### 2022-12-13, Version 3.3.7.1

* New: Option to assign object being explored into a public variable
* New: Name of the public variable is set in the options page of Thor Configuration
* New: The variable is released when Object Explorer closes.
* New: Right-click context menu in the TreeView on left allows you to assign any object in the tree into the public variable

### 2022-11-23, Version 3.3.6

* New: Double click now works on any column in the grid to edit property values.  
* New: Right-click on grid row brings up context menu to edit property (same as double-click) or to toggle whether the property is a favorite
* New: Checkbox for whether favorites are highlighted (with yellow highlighter)

### 2022-05-22, Version 3.3.3

* Add a TRY in TreeContainer.Init so no error if _Screen.oISXOptions.oKeyWordList doesn't exist

### 2022-04-04

* Updated the download zip file so it works with Thor Check for Updates.

### 2022-04-03

* Fixes a bug with an object based on Control in a running form and updates the displayed version number.

### 2022-04-02

* Fixes a bug with null nodes, such as with an object based on Empty or Control.

### 2022-03-04

* Automatically closes when the object being explored is released, and no longer prevents the object from being released

### 2022-03-03

* Changed hard-coded "explorer.vcx" in NEWOBJECT() statements to This.ClassLibrary so pathing isn't an issue.

* Set AllowOutput to .F. in forms.

* Ensure the explorer form is visible by being positioned within _screen or the active form, depending on the setting of ShowWindow.

* Added support for Thor Check For Updates.

### 2022-02-28

* Initial release: Jim and Matt's original version plus the following changes by Doug Hennig:

    * TreeContainer.NewNode: accept toParentNode and use the appropriate version of Nodes.Add if it's an object. This change (and the next one) prevents cashing for some objects (the TreeView was getting cranky when changing the Parent property of a node).

    * TreeContainer.LoadObject and LoadOtherObject: call NewNode rather than using their own Nodes.Add calls.

    * TreeContainer.NavigateToObject: specifically set loNode.Expanded to .T. because calling Expand doesn't necessary do that, also call loNode.EnsureVisible to ensure the TreeView is scrolled to the node. This fixes an issue with not automatically selecting the correct object in the TreeView at startup: the form was selected rather than the object, although the grid showed the properties for the correct object.

    * TreeContainer.InputBox: use InputBox_ShowWindow1 if the form is in a top-level form, not if it's modal. This change (and the next one) allows Object Explorer to be used in apps with _screen.Visible set to .F. and using top-level forms.

    * InputBox_ShowWindow1: set Desktop to .T. This doesn't affect apps with _screen.Visible =  .T. but does allow it to work when it's .F.


EndText

Return lcNote

EndProc 
